/*
 * GhostBot, a Discord bot made for all your Danny Phantom needs
 *     Copyright (C) 2018  Duncan "duncte123" Sterken
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'me.duncte123.ghostBot'
version "1.12.1_${getGitHash()}"
mainClassName = "${group}.GhostBot"

sourceCompatibility = JavaVersion.VERSION_1_10

repositories {
    jcenter()

    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

dependencies {
    //My little utils
    compile group: 'com.github.duncte123', name: 'botCommons', version: 'fbb8f98'
    //JDA
    compile(group: 'net.dv8tion', name: 'JDA', version: '3.8.1_439') {
        exclude module: 'opus-java'
    }
    //LavaPlayer/Lavalink
    compile group: 'com.github.FredBoat', name: 'Lavalink-Client', version: 'fbd2975'
    // Logback classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    runtime group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '3.0.0-alpha-3'
}

task sourcesForRelease(type: Copy) {
    from('src/main/java') {
        include '**/Variables.java'
        filter(ReplaceTokens, tokens: [
                ghostBotVersion: project.version.toString()
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'Variables.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source

    options.encoding = 'UTF-8'

    dependsOn generateJavaSources
}

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

shadowJar {
    version = null
    classifier = null
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter jar
}